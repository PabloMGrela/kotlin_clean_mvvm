apply plugin: 'jacoco'

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("../build/reports/jacoco") // file for reports (xml, html)
}

ext {
    coverageSourceDirs = 'src/test/java'
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/mainapp/**',
            '**/*Activity*.*',
            '**/*Fragment*.*',
            '**/*View*.*',
            '**/*Layout*.*',
            '**/*Adapter*.*',
            '**/*Api*.*',
            '**/*Modules*.*'
    ]

    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    def presentationKotlinClassTree = fileTree(dir: "../presentation/build/tmp/kotlin-classes/debug/", excludes: fileFilter)
    def presentationSrc = "../presentation/src/main/java"

    def domainKotlinClassTree = fileTree(dir: "../domain/build/classes/kotlin/", excludes: fileFilter)
    def domainSrc = "../domain/src/main/java"

    def dataKotlinClassTree = fileTree(dir: "../data/build/classes/kotlin/", excludes: fileFilter)
    def dataSrc = "../data/src/main/java"

    def localKotlinClassTree = fileTree(dir: "../local_datasource/build/tmp/kotlin-classes/debug/", excludes: fileFilter)
    def localSrc = "../local-datasource/src/main/java"

    def remoteKotlinClassTree = fileTree(dir: "../remote_datasource/build/tmp/kotlin-classes/debug/", excludes: fileFilter)
    def remoteSrc = "../remote-datasource/src/main/java"

    getClassDirectories().setFrom(files(
            [debugTree],
            [presentationKotlinClassTree],
            [domainKotlinClassTree],
            [dataKotlinClassTree],
            [localKotlinClassTree],
            [remoteKotlinClassTree]
        )
    )
    getSourceDirectories().setFrom(files(
            [mainSrc],
            [presentationSrc],
            [domainSrc],
            [dataSrc],
            [localSrc],
            [remoteSrc]
        )
    )
    getExecutionData().setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}